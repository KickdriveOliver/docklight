' Docklight Scripting - Example Script
' Converted to standard .txt/.vbs format - Original file name: report_splitter_example_auto.pts
' The original .pts file start with 3 extra lines before the VBScript code: DL_SCRIPTVERSION token, version number (1), checksum):
' DL_SCRIPTVERSION
' 1
' 24641

' report_splitter_example.ptp / report_splitter_example_auto.pts
' Oliver Heggelbacher / 2025-01-17

' precondition: USBHID settings with "I" (report ID) mode.

MAX_REPORT_LENGTH = 63

Do	
	DL.Pause 1 ' (the pause reduces CPU load while idle)
Loop

Sub DL_OnSend()
    If DL.OnSend_GetName() = "" Then
        ' do not "re-process" the nameless Send Sequences generated by the code below 
        Exit Sub
    End If

    ' Expected sequence data is WITHOUT leading report ID. Payload data only. 
    dataSize = DL.OnSend_GetSize()
    reportIdStr = "01 "
    If (dataSize <= MAX_REPORT_LENGTH) Then 
        ' single report, just prepend report ID to the existing data
        DL.AddComment "Simple report (ID = 01)"
        DL.OnSend_SetData reportIdStr & DL.OnSend_GetData("H"), "H"
        ' and done
        Exit Sub
    End If

    ' Multiple reports needed:
    idx = 1
    reportSize = 0 
    Do 
        If (idx = 1) Then 
            reportSize = MAX_REPORT_LENGTH
            reportIdStr = "02 "
            DL.AddComment "First report (ID = 02, size = " & reportSize & ")"
        ElseIf (idx + MAX_REPORT_LENGTH > dataSize) Then
            reportSize = dataSize - idx + 1
            reportIdStr = "04 "
            DL.AddComment "Final report (ID = 04, size = " & reportSize & ")"
        Else 
            reportSize = MAX_REPORT_LENGTH
            reportIdStr = "03 "
            DL.AddComment "Intermediate report (ID = 03, size = " & reportSize & ")"
        End If
        DL.SendSequence "", reportIdStr & DL.OnSend_GetData("H", idx, reportSize), "H"
        idx = idx + reportSize
    Loop Until idx >= dataSize
    ' Now clear the original sequence data. It should be suppressed completely. 
    DL.OnSend_SetData "", "H"
    ' The newly generated sequences are now in the OnSend() queue and will be processed subsequently.
End Sub

